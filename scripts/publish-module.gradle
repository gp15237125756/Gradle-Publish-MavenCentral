apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    println 'sourceSets.main.java.srcDirs ======' +  project.plugins.findPlugin("com.android.library")
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        println 'sourceSets.main.java.srcDirs ======' + sourceSets.main.java.srcDirs
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

ext {
    PUBLISH_GROUP_ID = 'io.github.Kino521'
    PUBLISH_VERSION = '1.0.0'
    PUBLISH_ARTIFACT_ID = 'next-sdk'
    PUBLISH_DESCRIPTION = 'NEXT Android SDK'
    PUBLISH_URL = 'https://github.com/Kino521/Hilt'
    PUBLISH_LICENSE_NAME = 'GNU LESSER GENERAL PUBLIC LICENSE Version 3'
    PUBLISH_LICENSE_URL =
            'https://github.com/elye/loaderviewlibrary/blob/master/LICENSE'
    PUBLISH_DEVELOPER_ID = 'Kino521'
    PUBLISH_DEVELOPER_NAME = 'Kino521'
    PUBLISH_DEVELOPER_EMAIL = 'zhikai.jin@bozhon.com'
    PUBLISH_SCM_CONNECTION =
            'scm:git:github.com/Kino521/Hilt.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION =
            'scm:git:ssh://github.com/Kino521/Hilt.git'
    PUBLISH_SCM_URL =
            'https://github.com/Kino521/Hilt/tree/master'
}
group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = PUBLISH_DESCRIPTION
                    url = PUBLISH_URL
                    licenses {
                        license {
                            name = PUBLISH_LICENSE_NAME
                            url = PUBLISH_LICENSE_URL
                        }
                    }
                    developers {
                        developer {
                            id = PUBLISH_DEVELOPER_ID
                            name = PUBLISH_DEVELOPER_NAME
                            email = PUBLISH_DEVELOPER_EMAIL
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = PUBLISH_SCM_CONNECTION
                        developerConnection = PUBLISH_SCM_DEVELOPER_CONNECTION
                        url = PUBLISH_SCM_URL
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}